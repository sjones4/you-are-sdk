/*
 * This build file was auto generated by running the Gradle 'buildSetup' task
 * by 'steve' at '11/21/13 6:38 PM' with Gradle 1.8
 *
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at http://gradle.org/docs/1.8/userguide/tutorial_java_projects.html
 */

plugins {
    id 'java'
    id 'java-library'
    id 'idea'
    id 'maven-publish'
}

group = 'com.github.sjones4'
version = '1.2.2'
targetCompatibility = 1.7
sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    api 'com.amazonaws:aws-java-sdk-core:[1.11, 1.12)'
    api 'com.amazonaws:aws-java-sdk-ec2:[1.11, 1.12)'
    api 'com.amazonaws:aws-java-sdk-iam:[1.11, 1.12)'
    api 'com.amazonaws:aws-java-sdk-sts:[1.11, 1.12)'
}

jar {
  manifest {
    attributes("Implementation-Title": "you-are-sdk", "Implementation-Version": project.version)
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Sdk(MavenPublication) {
            from components.java
            groupId 'com.github.sjones4'
            artifactId 'you-are-sdk'
            version project.version
            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/sjones4/you-are-sdk")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USER")
                password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}


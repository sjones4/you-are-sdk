/*
 * This build file was auto generated by running the Gradle 'buildSetup' task
 * by 'steve' at '11/21/13 6:38 PM' with Gradle 1.8
 *
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at http://gradle.org/docs/1.8/userguide/tutorial_java_projects.html
 */

plugins {
    id 'java'
    id 'idea'
    id 'com.jfrog.bintray' version '1.2'
    id 'maven'
    id 'maven-publish'
}

group = 'com.github.sjones4'
version = '1.2.2'
targetCompatibility = 1.7
sourceCompatibility = 1.7

repositories {
    jcenter()
}

dependencies {
    compile 'com.amazonaws:aws-java-sdk-core:1.11.+'
    compile 'com.amazonaws:aws-java-sdk-ec2:1.11.+'
    compile 'com.amazonaws:aws-java-sdk-iam:1.11.+'
    compile 'com.amazonaws:aws-java-sdk-sts:1.11.+'
}

jar {
  manifest {
    attributes("Implementation-Title": "you-are-sdk", "Implementation-Version": version)
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Publication(MavenPublication) {
            from components.java
            groupId 'com.github.sjones4'
            artifactId 'you-are-sdk'
            version version
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Publication']
    pkg {
        repo = 'maven'
        name = 'com.github.sjones4:you-are-sdk'
        desc = 'Extension of AWS SDK for Java with Eucalyptus specific functionality'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/sjones4/you-are-sdk.git'
        websiteUrl = 'https://github.com/sjones4/you-are-sdk'
        issueTrackerUrl = 'https://github.com/sjones4/you-are-sdk/issues'
    }
}

